##
# simple scons file for build

Import('*')	# import SConstruct exported var
import os

gfal_common_libs = [ 'vomsc','is_ifce'] 		# libraries linked
gsimplecache = ["externals/gsimplecache/gsimplecacheOS.os"]
other_headers=['common/']
common_src = Glob("common/*.c") 
common_voms = Glob("common/voms/*.c")
common_mds = Glob("common/mds/*.c") 
posix_src_all = Glob("posix/*.c")
src_all = common_src + common_mds + common_voms + posix_src_all

plugin_lfc = Glob("common/lfc/*.c")
plugin_srm = Glob("common/srm/*.c")
plugin_rfio = Glob("common/rfio/*.c")
plugin_dcap = Glob("common/dcap/*.c")

##
# Clone the env for the link flag

env_posix = env.Clone()
env_posix.Append(LIBS=gfal_common_libs)
env_posix.Append(CPPPATH=other_headers)

#main gfal lib
mainlib = env_posix.SharedLibrary('#build/libs/libgfal-2.0', src_all)
staticlib = env_posix.StaticLibrary('#build/libs/libgfal-2.0', src_all)
#plugins
env_plugin = env.Clone();
env_plugin.Append(LIBS=["gfal-2.0"])
env_plugin_srm = env_plugin.Clone()
env_plugin_srm.Append(LIBS=["gfal_srm_ifce"] )
env_plugin_lfc = env_plugin.Clone();
env_plugin_lfc.Append(CFLAGS="-D_REENTRANT")
plugin_lfc_lib = env_plugin_lfc.SharedLibrary('#build/libs/libgfal_plugin_lfc', plugin_lfc+ gsimplecache)
plugin_rfio_lib= env_plugin.SharedLibrary('#build/libs/libgfal_plugin_rfio', plugin_rfio)
plugin_dcap_lib = env_plugin.SharedLibrary('#build/libs/libgfal_plugin_dcap', plugin_dcap)
plugin_srm_lib = env_plugin_srm.SharedLibrary('#build/libs/libgfal_plugin_srm', plugin_srm + gsimplecache)

#gfal version
version = env_plugin.Program("#build/gfal_version", Glob("version/*.c"))

if ARGUMENTS.get('package','0') =='yes':
	print "PACKAGING mode MODE"
	#print "Architecture : " +ctypes.sizeof(ctypes.c_voidp)*8 + "bits"
	env_posix.Install('/usr/lib/', '#build/libs/libgfal-2.0.so')
	env_posix.Package(
				 NAME     = 'gfal-2.0',
				 VERSION        = '2.0_preview',
				 PACKAGEVERSION = 0,
				 PACKAGETYPE    = 'rpm',
				 LICENSE        = 'Apache',
				 SUMMARY        = 'grid file access library 2.0',
				 DESCRIPTION    = 'Provide a POSIX like API to manage file and directory with the multiples protocols of the grid',
				 X_RPM_GROUP    = 'CERN/grid',
				 SOURCE_URL     = 'https://svnweb.cern.ch/trac/lcgutil/wiki'
			)


Default(plugin_rfio_lib, plugin_dcap_lib, plugin_lfc_lib, plugin_srm_lib, mainlib,staticlib, version)

